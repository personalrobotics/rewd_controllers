cmake_minimum_required(VERSION 2.8.3)
project(rewd_controllers)

set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")

include_directories("include")

#==============================================================================
# Dependencies
#
find_package(catkin REQUIRED COMPONENTS
  actionlib
  control_msgs
  control_toolbox
  controller_interface
  hardware_interface
  pr_control_msgs
  pr_hardware_interfaces
  realtime_tools
)
include_directories(SYSTEM ${catkin_INCLUDE_DIRS})

if ("${controller_interface_VERSION}" VERSION_LESS 0.10.0)
  message(FATAL_ERROR "controller_interface package must be at least version 0.10.0, not packaged in Indigo. Please upgrade to Jade or Kinetic or build the ros_control packages from source.") 
endif()

find_package(aikido REQUIRED COMPONENTS common control control_ros io statespace trajectory)
include_directories(SYSTEM ${aikido_INCLUDE_DIRS})

find_package(DART REQUIRED COMPONENTS utils utils-urdf CONFIG)
include_directories(SYSTEM ${DART_INCLUDE_DIRS})

#==============================================================================
# Catkin Configuration
#
catkin_package(
  CATKIN_DEPENDS
    actionlib
    control_msgs
    control_toolbox
    controller_interface
    hardware_interface
    pr_control_msgs
    pr_hardware_interfaces
    realtime_tools
  DEPENDS
    aikido
    DART
  INCLUDE_DIRS
    include
  LIBRARIES
    "${PROJECT_NAME}"
)

#==============================================================================
# Targets
#
add_library("${PROJECT_NAME}" SHARED
  src/JointGroupPositionController.cpp
  src/GravityCompensationController.cpp
  src/helpers.cpp
  src/JointAdapter.cpp
  src/JointAdapterFactory.cpp
  src/JointTrajectoryControllerBase.cpp
  src/JointTrajectoryController.cpp
  src/MoveUntilTouchController.cpp
)
target_link_libraries("${PROJECT_NAME}"
  ${DART_LIBRARIES}
  ${aikido_LIBRARIES}
  ${catkin_LIBRARIES}
)

#==============================================================================
# Installation
#
install(DIRECTORY "include"
  DESTINATION "${CATKIN_PACKAGE_INCLUDE_DESTINATION}"
)
install(TARGETS "${PROJECT_NAME}"
  ARCHIVE DESTINATION "${CATKIN_PACKAGE_LIB_DESTINATION}"
  LIBRARY DESTINATION "${CATKIN_PACKAGE_LIB_DESTINATION}"
  RUNTIME DESTINATION "${CATKIN_PACKAGE_BIN_DESTINATION}"
)
install(FILES "rewd_controllers_plugins.xml"
  DESTINATION "${CATKIN_PACKAGE_SHARE_DESTINATION}"
)
